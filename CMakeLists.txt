cmake_minimum_required(VERSION 3.2.0)

project(text-pango)

# Standalone build
if(NOT OBS_OUTPUT_DIR)
	set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/")
	include(ObsHelpers)

	set(CMAKE_C_STANDARD 11)
	set(STANDALONE_PLUGIN TRUE)

	find_path(OBS_INCLUDE_DIRS
		obs-module.h
		HINTS $ENV{OBS_DIR} ${OBS_DIR}
		PATH_SUFFIXES
			include
			libobs)
	find_library(OBS_LIBRARIES
		NAMES obs libobs
		HINTS $ENV{OBS_DIR} ${OBS_DIR}
		PATH_SUFFIXES
			install/lib
			lib
			install/bin/64bit
			libobs)
# Build as submodule of obs-studio
else()
	set(OBS_INCLUDE_DIRS
		../../libobs)
	set(OBS_LIBRARIES
		libobs)
	set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/plugins/obs-text-pango/cmake/")
endif()

find_package(Pango REQUIRED)
find_package(Cairo REQUIRED)
find_package(Pangocairo REQUIRED)
find_package(Pangoft2 REQUIRED)
if(WIN32)
	find_package(Pangowin32 REQUIRED)
endif()
find_package(Fontconfig REQUIRED)
find_package(Freetype REQUIRED)

include_directories(
	${OBS_INCLUDE_DIRS}
	${PANGO_INCLUDE_DIRS}
	${CAIRO_INCLUDE_DIRS}
	${PANGOCAIRO_INCLUDE_DIRS}
	${PANGOFT2_INCLUDE_DIRS}
	${FONTCONFIG_INCLUDE_DIRS}
	${FREETYPE_INCLUDE_DIRS})
FIND_PACKAGE(PkgConfig)
if(PKG_CONFIG_FOUND)
	PKG_CHECK_MODULES(GLIB glib-2.0 REQUIRED)
	include_directories(${GLIB_INCLUDE_DIRS})
endif()

link_directories(
	${PANGO_LIBRARY_DIRS}
	${CAIRO_LIBRARY_DIRS}
	${FONTCONFIG_LIBRARY_DIRS}
	${FREETYPE_LIBRARY_DIRS})

set(text-pango_SOURCES
	text-pango.c
	text-pango.h
	text-utilities.h)

set(win32_static_LIBRARIES_NAMES
	intl
	fribidi
	pixman-1
	z
	harfbuzz
	png16
	expat
	usp10)

if(WIN32)
	foreach(lib_name ${win32_static_LIBRARIES_NAMES})
		# Skip resolving usp10 as it's a system library
		if(lib_name STREQUAL "usp10")
			list(APPEND PLATFORM_LIBRARIES ${lib_name})
			continue()
		endif()

		find_library(LIB_PATH_${lib_name}
			NAMES ${lib_name} lib${lib_name}
			HINTS ${CMAKE_PREFIX_PATH}
			PATH_SUFFIXES lib bin
		)
		if(LIB_PATH_${lib_name})
			list(APPEND PLATFORM_LIBRARIES ${LIB_PATH_${lib_name}})
		else()
			message(WARNING "Could not find library ${lib_name}, falling back to bare name")
			list(APPEND PLATFORM_LIBRARIES ${lib_name})
		endif()
	endforeach()
endif()

add_library(text-pango MODULE
	${text-pango_SOURCES})
target_link_libraries(text-pango
	${OBS_LIBRARIES}
	${CAIRO_LIBRARIES}
	${PANGO_LIBRARIES}
	${PANGOWIN32_LIBRARIES}
	${PANGOCAIRO_LIBRARIES}
	${PANGOFT2_LIBRARIES}
	${FREETYPE_LIBRARIES}
	${FONTCONFIG_LIBRARIES}
	${PLATFORM_LIBRARIES}) # win only currently
target_compile_definitions(text-pango PRIVATE ${CAIRO_C_FLAGS})

if(STANDALONE_PLUGIN)
	if(CMAKE_SIZEOF_VOID_P EQUAL 8)
		set(_bit_suffix "64bit/")
	else()
		set(_bit_suffix "32bit/")
	endif()
	if(APPLE)
		install(TARGETS text-pango
			LIBRARY DESTINATION "Library/Application Support/obs-studio/plugins/libtext-pango/bin/")
		install(DIRECTORY data/
			DESTINATION "Library/Application Support/obs-studio/plugins/libtext-pango/data")
		install(DIRECTORY apple/
			DESTINATION "Library/Application Support/obs-studio/plugins/libtext-pango/data")
		# for packaging, copy all dependencies, change dependency links
		# run pkgbuild --identifier user.obsproject.kurufu.text-pango --root ./obs-text-pango-install/ text-pango-mac.pkg
	elseif(UNIX AND NOT APPLE)
		install(TARGETS text-pango 
			LIBRARY DESTINATION "bin/")
		install(DIRECTORY data/
			DESTINATION data/)
	else() # Not apple so normal layout
		install(TARGETS text-pango
			LIBRARY DESTINATION "obs-plugins/${_bit_suffix}/")
		install(DIRECTORY data/
			DESTINATION data/obs-plugins/text-pango)
		# Platform specific data, like fontconfig in win32
		install(FILES $<TARGET_PDB_FILE:${PROJECT_NAME}>
			DESTINATION "obs-plugins/${_bit_suffix}/" OPTIONAL)
		install(DIRECTORY win32/
			DESTINATION data/obs-plugins/text-pango)
	endif()
else()
	set_target_properties_obs(text-pango PROPERTIES FOLDER plugins/text-pango PREFIX "")
	# Might not work for directories...
	if(WIN32)
		target_add_resource(text-pango win32)
	endif()
	if(APPLE)
		target_add_resource(text-pango apple)
	endif()
endif()

